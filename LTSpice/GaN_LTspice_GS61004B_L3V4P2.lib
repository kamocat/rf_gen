* Created in LTspice Version XVII                               *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Version 4.2                                                   *
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
*   GS61004B                                                    *
*****************************************************************
*Level 3. In addition to Level 2, this level includes the stray inductance in the package.
*$
*$
.subckt GaN_LTspice_GS61004B_L3V4P2 gatein drainin sourcein source_S TC TJ
*
*
.param	rTC=-0.004	gan_res={9.09e-3}	metal_res={4.5e-3}	gtc=2.81	sh_s=0.05263	sh_d=0.94376
.param	cur=0.0915	x0_0=1.1	x0_1=1.1	x0_2=1.0	thr=1.8	itc=0.384	atc=80.94454451
.param	sfC = 30
*
*
Cth_1 0 TJ 		{2.5e-5 * sfC}
Cth_2 0 T11  	{2.0e-4 * sfC}
Cth_3 0 T22 	{1.9e-3 * sfC}
Cth_4 0 T33 	{0.9e-3 * sfC}
*
*
Rth_1 T11 TJ 	{0.045}
Rth_2 T22 T11 	{0.55}
Rth_3 T33 T22 	{0.57}
Rth_4 TC T33 	{0.035}
*
*
bdtemp 0 TJ I = (if(v(drain,source)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source)-thr)))*
+	v(drain,source)/(1 + max(x0_0+x0_1*(v(gate,source)+x0_2),0.2)*v(drain,source)))*
+	v(drainin,sourcein),
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain)-thr)))*
+	v(source, drain)/(1 + max(x0_0+x0_1*(v(gate,drain)+x0_2),0.2)*v(source,drain)))*
+	v(sourcein, drainin)))
*
*
ld drainin drain3 {170e-12} Rser=0
ERES_d drain3 3_d value={I(VSENSE_d)*sh_d* (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc))}
VSENSE_d 3_d drain DC 0
*
*
VSENSE_s 3_s source DC 0
ERES_s source3 3_s value={I(VSENSE_s)* sh_s * (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc))}
Lcs source3 source3b {20e-12}
Ls sourcein source3b {4.0e-11} Rser=0
*
*
RSS source_S1 source3b {0.001}
LSS source_S source_S1 {0.2e-9} Rser=0
rg gatein gate1 {1.5}
Lg gate1 gate {2.5e-9} Rser=0
*
*
Rcsdconv drain source {4000Meg}
Rcgsconv gate source {4000Meg}
Rcgdconv gate drain {4000Meg}
*
*
bswitch drain2 source2 I =	(if(v(drain2,source2)>0,
+   						(cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source2)-thr)))*
+							v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+x0_2),0.2)*v(drain2,source2))),
+   						(-cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain2)-thr)))*
+							v(source2, drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+x0_2),0.2)*v(source2,drain2)))))
*
*
R_drain2 drain2 drain {(1e-7)}
R_source2 source2 source {(1e-7)}
*
*
C_GS	gate source	{2.515e-10}
C_GS1	gate source	Q = ( 	- 1.7825e-10*(1-1/(1+exp(0.115*(-v(drain, source)-8.0))))
+							- 3.5e-12*(1-1/(1+exp(0.02*(-v(drain, source)+80.0))))
+							- 2.1e-10*(-1+1/(1+exp(0.19*(-v(drain, source)-10)))) )*x
*
*
C_GS2	gate source	Q = ( 	0.52e-010*log(1+exp(4.5*(x-2)))	)
*
*
C_GD	gate drain	{2.8e-012}
C_GD1	gate drain	Q = (	15.5e-10*log(1+exp(0.115*(x-8)))
+							+ 5e-11*log(1+exp(0.0429*(x+80)))	)
*
*
C_SD	source drain	{0.9e-010}
C_SD1	source drain	Q = (	3.15e-10*log(1+exp(0.5*(x+16)))
+								+ 1.69e-9*log(1+exp(0.03*(x+31)))	)
*
*
.ends
*$
